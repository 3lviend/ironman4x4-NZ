$(document).on 'page:load ready', ->
  addedToWishlist = '<%= I18n.t('refinery.ironman.products.index.buttons.added_to_wishlist') %>';
  getItemsFromWishlist = ->
    wishlist = $.cookie('wishlist')
    w = if wishlist? then wishlist.split(',') else []
    items = {}
    _.each w, (item) ->
      a = item.split '|'
      items[a[0]] = Number a[1]
    items

  saveItemsToWishlist = (items) ->
    a = _.map items, (quantity, productId) ->
      productId + '|' + quantity
    b = a.join ','
    $.cookie 'wishlist', b, {expires:7, path: '/'}

  addItemToWishlist = (productId, quantity = 1) ->
    items = getItemsFromWishlist()
    items[productId] = (items[productId] ? 0) + quantity
    saveItemsToWishlist items

  updateQuantity = (productId, quantity) ->
    items = getItemsFromWishlist()
    items[productId] = quantity
    saveItemsToWishlist items

  removeItemFromWishlist = (productId) ->
    items = getItemsFromWishlist()
    delete items[productId]
    saveItemsToWishlist items

  $('#page-product-list .btn-add-to-wishlist').on 'click', ->
    addItemToWishlist $(@).data('product-id')
    $(@).text(addedToWishlist).addClass 'disabled added'
    document.location.reload true

  items = getItemsFromWishlist()
  _.each items, (quantity, productId) ->
    $("#page-product-list .btn-add-to-wishlist[data-product-id=#{productId}]").text(addedToWishlist).addClass 'disabled'

  $('#wishlist .update-wishlist.btn').on 'click', ->
    $('#wishlist input.quantity').each ->
      productId = $(@).data('product-id')
      quantity = parseInt $(@).val()
      if isNaN(quantity) then quantity = 1
      if quantity == 0
        removeItemFromWishlist productId
      else
        $(@).val quantity
        updateQuantity productId, quantity
    document.location.reload true

  $('#wishlist .remove-item.btn').on 'click', ->
    productId = $(@).data('product-id')
    removeItemFromWishlist productId
    document.location.reload true

  $('#wishlist .next.btn').on 'click', ->
    nextTab = $(@).data('next-tab')
    $('#wishlist ul[role=tablist] a[href="#' + nextTab + '"]').tab('show').focus().blur()

  setClassesForTabs = (active) ->
    currentIndex = 0

    $('#wishlist ul[role=tablist] a').each (i, tab) ->
      if tab == active
        currentIndex = i

    $('#wishlist ul[role=tablist] li').each (i) ->
      if i < currentIndex
        $(@).addClass 'completed'
      else
        $(@).removeClass 'completed'
      if i > currentIndex
        $(@).addClass 'incomplete'
      else
        $(@).removeClass 'incomplete'

  $('#wishlist a[data-toggle="tab"]').on 'shown.bs.tab', (e) ->
    setClassesForTabs e.target

  active = $('#wishlist a[data-toggle="tab"].active').get(0)
  setClassesForTabs active
