$(document).on 'page:load ready', ->
  addedToWishlist = '<%= I18n.t('refinery.ironman.products.index.buttons.added_to_wishlist') %>';
  getItemsFromWishlist = ->
    wishlist = $.cookie('wishlist')
    w = if wishlist? then wishlist.split(',') else []
    items = {}
    _.each w, (item) ->
      a = item.split '|'
      items[a[0]] = Number a[1]
    items

  saveItemsToWishlist = (items) ->
    a = _.map items, (quantity, productId) ->
      productId + '|' + quantity
    b = a.join ','
    $.cookie 'wishlist', b, {expires:7, path: '/'}

  addItemToWishlist = (productId, quantity = 1) ->
    items = getItemsFromWishlist()
    if productId && not isNaN(quantity)
      newQuantity = Number((items[productId] ? 0) + quantity)
      items[productId] = (if isNaN(newQuantity) then 0 else newQuantity)
      saveItemsToWishlist items

  updateQuantity = (productId, quantity) ->
    items = getItemsFromWishlist()
    items[productId] = quantity
    saveItemsToWishlist items

  removeItemFromWishlist = (productId) ->
    items = getItemsFromWishlist()
    delete items[productId]
    saveItemsToWishlist items

  $('#page-product-list, #page-product').on 'click', '.btn-add-to-wishlist', ->
    addItemToWishlist $(@).data('product-id')
    $(@).text(addedToWishlist).addClass 'disabled added'
    document.location.reload true

  applyAddToWishlistButton = ->
    if $('#page-product-list, #page-product').length
      items = getItemsFromWishlist()
      _.each items, (quantity, productId) ->
        if productId? && productId
          $('#page-product-list, #page-product').find(".btn-add-to-wishlist[data-product-id=#{productId}]").text(addedToWishlist).addClass 'disabled'

  $('#wishlist').on 'click', '.update-wishlist.btn', ->
    $('#wishlist input.quantity').each ->
      productId = $(@).data('product-id')
      quantity = parseInt $(@).val()
      if isNaN(quantity)
        quantity = 1
      if quantity == 0
        removeItemFromWishlist productId
      else
        $(@).val quantity
        updateQuantity productId, quantity
    document.location.reload true

  $('#wishlist').on 'click', '.remove-item.btn', ->
    productId = $(@).data('product-id')
    removeItemFromWishlist productId
    document.location.reload true

  $(document).on 'pagination:onafterpageload', ->
    applyAddToWishlistButton()

  applyAddToWishlistButton()

  $('#product-list-condensed .product').each ->
    specs = $(@).data 'specifications'
    $(@).popover({
      placement: 'top'
      html: true
      content: ->
        specs = $(@).data 'product-specifications'
        out = ''
        out += '<table class="table table-condensed table-noborder">'
        _.each specs, (value, title) ->
          out += '<tr><th>' + title + '</th><td>' + value + '</td></tr>'
        out += '</table>'
        out += '<div>'
        out += '<a href="' + $(@).data('product-url') + '" class="btn btn-primary btn-sm">View Product</a> '
        out += '<button class="btn btn-primary btn-sm">Add to Quote</button>'
        out += '</div>'
        out
      trigger: 'hover'
    })
    # move position of popover, so it buttons on it can more easily be pressed
    .on 'shown.bs.popover', (event) ->
      position = $(@).position()
      $tip = $(@).data('bs.popover').$tip
      $tip.offset(top:position.top-$tip.outerHeight(), left:position.left+$(@).width()-$tip.outerWidth())
      $(@).append $tip
