$(document).on 'page:load ready', ->
  cookieOptions = {expires:7, path: '/'}

  headings = {
    '#wishlist-review': '<%= I18n.t('refinery.ironman.orders.form.headings.wishlist_review') %>'
    '#wishlist-details': '<%= I18n.t('refinery.ironman.orders.form.headings.wishlist_details') %>'
    '#wishlist-stockist': '<%= I18n.t('refinery.ironman.orders.form.headings.wishlist_stockist') %>'
    '#wishlist-submission': '<%= I18n.t('refinery.ironman.orders.form.headings.wishlist_submission') %>'
  }

  $('#wishlist.form .next.btn').on 'click', ->
    active = $('#wishlist li.active a[data-toggle="tab"]').attr('href')

    validations = {
      '#wishlist-review': -> true
      '#wishlist-details': ->
        ok = true

        vehicleData = {
          vehicle_make_id: Number $('#vehicle_1st_id').val()
          vehicle_model_id: Number $('#vehicle_2nd_id').val()
          vehicle_series_id: Number $('#vehicle_3rd_id').val()
        }

        if vehicleData.vehicle_make_id isnt 0 and (vehicleData.vehicle_model_id is 0 or vehicleData.vehicle_series_id is 0)
          alert('<%= I18n.t('layouts.fit_my_4x4.warning') %>')
          ok = false

        else
          $('input[required=required],textarea[required=required],select[required=required]', active).each ->
            if not $(@).val()
              ok = false

          if not ok
            alert '<%= I18n.t('refinery.ironman.orders.form.required_fields_missing') %>'
          else
            if not $('#terms_conditions').is(':checked')
              alert '<%= I18n.t('refinery.ironman.orders.contact_details.terms_conditions.warning') %>'
              ok = false

        return ok
      '#wishlist-stockist': -> true
    }

    if not validations[active]? or validations[active]()
      nextTab = $(@).data('next-tab')
      $('#wishlist ul[role=tablist] a[href="#' + nextTab + '"]').tab('show').focus().blur()

  $('#wishlist-details :input').on 'change', ->
    vehicleData = {
      vehicle_make_id: Number $('#vehicle_1st_id').val()
      vehicle_model_id: Number $('#vehicle_2nd_id').val()
      vehicle_series_id: Number $('#vehicle_3rd_id').val()
    }

    if not (vehicleData.vehicle_make_id is 0 or vehicleData.vehicle_model_id is 0 or vehicleData.vehicle_series_id is 0)
      $.cookie 'vehicle_details', JSON.stringify(vehicleData), cookieOptions

    if not window.localStorage?
      return
    else
      localStorage.setItem 'wishlist-details', JSON.stringify $(@).closest('form').serializeObject()

  if window.localStorage?
    data = JSON.parse(localStorage.getItem('wishlist-details') ? '{}')
    _.each data, (value, key) ->
      if key.match('order|terms_conditions')
        el = $("#wishlist [name=\"#{key}\"]")
        if el.not('[type=hidden]').length
          if el.is(':checkbox')
            el.prop 'checked', (value isnt '0')
          else
            el.val value

  setClassesForTabs = (active) ->
    currentIndex = 0

    $('#wishlist ul[role=tablist] a').each (i, tab) ->
      if tab == active
        currentIndex = i

    $('#wishlist ul[role=tablist] li').each (i) ->
      if i < currentIndex
        $(@).addClass 'completed'
      else
        $(@).removeClass 'completed'
      if i > currentIndex
        $(@).addClass 'incomplete'
      else
        $(@).removeClass 'incomplete'

  $('#wishlist.form a[data-toggle="tab"]').on 'click', (e) ->
    if ($(e.target).closest('li').hasClass('incomplete') or $(e.target).closest('li').hasClass('disabled'))
      e.preventDefault()
      e.stopPropagation()

  $('#wishlist.form a[data-toggle="tab"]').on 'shown.bs.tab', (e) ->
    setClassesForTabs e.target
    $.cookie 'wishlist-tab', e.target.hash, cookieOptions
    $('#layout-heading .page-header').text headings[e.target.hash]

  stockistsInit = false

  tab = $.cookie 'wishlist-tab'
  if tab? and tab and $('#wishlist.form').length
    $('#wishlist ul[role=tablist] a[href="' + tab + '"]').tab('show').focus().blur()
    if tab is '#wishlist-review'
      setClassesForTabs active
    if tab is '#wishlist-stockist'
      setTimeout ->
        window.initStockists() if not stockistsInit
        stockistsInit = true
      , 1000
  else
    active = $('#wishlist li.active a[data-toggle="tab"]').get(0)
    setClassesForTabs active

  $('#wishlist.form a[data-toggle="tab"][href="#wishlist-stockist"]').on 'shown.bs.tab', (e) ->
    window.initStockists() if not stockistsInit
    stockistsInit = true

  $('#wishlist-stockist #stockists .stockist').each ->
    style = 'btn-info'
    text = '<%= I18n.t('refinery.ironman.orders.form.buttons.select') %>'

    stockistId = data['order[stockist_id]']
    thisStockistId = Number($('.heading', @).data('stockist-id'))
    if stockistId and Number(stockistId) is thisStockistId
      style = 'btn-primary'
      text = '<%= I18n.t('refinery.ironman.orders.form.buttons.selected') %>'
      $('#order_stockist_id').val stockistId

    $(@).append(
      '<div class="row">
        <div class="field contact col-sm-offset-2 col-sm-10">
          <button type="button" class="btn ' + style + ' btn-sm select-stockist" data-stockist-id="' + thisStockistId + '">' + text + '</button>
        </div>
      </div>'
    )

  $('#wishlist-stockist').on 'click', '.btn.select-stockist', ->
    btn = $(@)
    stockistId = $(@).data 'stockist-id'

    if btn.hasClass 'btn-primary'
      $('#order_stockist_id').val ''

      $('#wishlist-stockist .btn.select-stockist[data-stockist-id=' + stockistId + ']')
        .removeClass('btn-primary')
        .addClass('btn-info')
        .text('<%= I18n.t('refinery.ironman.orders.form.buttons.select') %>')
    else
      $('#wishlist-stockist .btn.select-stockist.btn-primary:not([data-stockist-id=' + stockistId + '])').each ->
        $(@).removeClass 'btn-primary'
        $(@).addClass 'btn-info'
        $(@).text '<%= I18n.t('refinery.ironman.orders.form.buttons.select') %>'

      $('#order_stockist_id').val stockistId

      $('#wishlist-stockist .btn.select-stockist[data-stockist-id=' + stockistId + ']')
        .removeClass('btn-info')
        .addClass('btn-primary')
        .text('<%= I18n.t('refinery.ironman.orders.form.buttons.selected') %>')

    $(@).blur()

    if not window.localStorage?
      return
    else
      localStorage.setItem 'wishlist-details', JSON.stringify $('#new_order').serializeObject()

  $('#wishlist-stockist .btn.print').on 'click', ->
    window.print?()

  $('#wishlist-stockist .btn.email').on 'click', ->
    if not $('#order_stockist_id').val()
      alert '<%= I18n.t('refinery.ironman.orders.form.please_select_a_stockist') %>'
    else
      $('#new_order').submit()

  $('#new_order input, #new_order select').keypress (event) ->
    return event.keyCode != 13
